plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
    id 'org.springframework.boot.experimental.thin-launcher' version '1.0.29.RELEASE'
    id 'org.hibernate.orm' version '6.1.6.Final'

    id 'checkstyle'
    id 'jacoco'

    id 'idea'
}

group = 'com.nytdacm'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    if (System.getenv("CI") == null) {
        // 本地开发时使用阿里云仓库
        maven { url 'https://maven.aliyun.com/repository/public/' }
        maven { url 'https://maven.aliyun.com/repository/spring/' }
    }
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

def satokenVersion = '1.34.0'
def hutoolVersion = '5.8.11'
def groovyVersion = '4.0.8'

dependencies {
    implementation "org.apache.groovy:groovy"
    implementation "cn.dev33:sa-token-spring-boot3-starter:$satokenVersion"
    implementation("cn.dev33:sa-token-jwt:$satokenVersion") {
        exclude module: 'hutool-core'
        exclude module: 'hutool-json'
    }
    implementation "cn.hutool:hutool-core:$hutoolVersion"
    implementation "cn.hutool:hutool-json:$hutoolVersion"
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    implementation 'org.jsoup:jsoup:1.15.3'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.2'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-lang:commons-lang:2.6'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.apache.groovy:groovy-test:$groovyVersion"
    testImplementation "org.apache.groovy:groovy-test-junit5:$groovyVersion"
}

tasks.named('test') {
    useJUnitPlatform()
}

hibernate {
    enhancement {
        lazyInitialization true
        dirtyTracking true
        associationManagement true
    }
}

idea {
    module {
        // 忽略前端构建文件
        excludeDirs += files("$projectDir/src/main/resources/static")
        excludeDirs += files("$projectDir/web/dist")
    }
}

tasks.named('check').configure {
    dependsOn jacocoTestReport
}
